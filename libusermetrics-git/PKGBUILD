# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=libusermetrics-git
_pkgname=libusermetrics
pkgver=r598.1.3.0
pkgrel=1
pkgdesc='Library for exporting anonymous metrics about users'
url='https://gitlab.com/ubports/development/core/libusermetrics'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3 LGPL GPL)
conflicts=(libusermetrics)
provides=(libusermetrics)
depends=(qt5-es2-base qt5-xmlpatterns gsettings-qt-git apparmor click-git qdjango-git libqtdbustest-git)
makedepends=(git cmake cmake-extras-lomiri doxygen intltool)
source=('git+https://gitlab.com/ubports/development/core/libusermetrics')
sha256sums=('SKIP')

BUILDENV+=('!check') # Some tests are broken

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
