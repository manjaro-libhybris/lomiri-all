# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=geonames-git
_pkgname=geonames
pkgver=r111.0.3.0
pkgrel=1
pkgdesc='Parse and query the geonames database dump'
url='https://gitlab.com/ubports/development/core/geonames'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(geonames)
provides=(geonames)
makedepends=(git gtk-doc)
source=('git+https://gitlab.com/ubports/development/core/geonames')
sha256sums=('SKIP')

BUILDENV+=('!check') # Weird locale issues

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var
  make -j8
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
