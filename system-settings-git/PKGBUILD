# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=system-settings-git
_pkgname=system-settings
pkgver=r7783.bf1c2d30
pkgrel=1
pkgdesc='System Settings application for Unity8'
url='https://github.com/ubports/system-settings'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL3)
conflicts=(system-settings)
provides=(system-settings)
depends=(trust-store-git click-git gnome-desktop ubuntu-app-launch-git geonames-git libqtdbusmock-git ubuntu-ui-toolkit-git gsettings-qt-git accountsservice-ubuntu upower lomiri-themes-git qmenumodel-git libqofono-qt5 content-hub-git)
makedepends=(git cmake intltool)
checkdepends=(xorg-server-xvfb)
source=('git+https://github.com/ubports/system-settings.git#branch=xenial'
        '0001-Use-ayatana-for-power-sound-and-datetime.patch'
        'no-deb-system-updates.patch')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

BUILDENV+=('!check') # Some tests fail

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd ${_pkgname}

  patch -Np1 -i "${srcdir}/0001-Use-ayatana-for-power-sound-and-datetime.patch"
  patch -Np1 -i "${srcdir}/no-deb-system-updates.patch"
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DENABLE_TESTS=off ..
  make
}

check() {
  cd ${_pkgname}/build
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
