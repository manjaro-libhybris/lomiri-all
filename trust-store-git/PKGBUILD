# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=trust-store-git
_pkgname=trust-store
pkgver=r488.6c37311
pkgrel=1
pkgdesc='C++11 library for persisting trust requests'
url='https://gitlab.com/ubports/development/core/trust-store'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(trust-store)
provides=(trust-store)
depends=(qt5-es2-base qt5-quickcontrols boost dbus-cpp gtest-lomiri lcov gcovr mir apparmor properties-cpp)
makedepends=(git cmake cmake-extras-lomiri doxygen graphviz)
source=('git+https://gitlab.com/ubports/development/core/trust-store')
sha256sums=('SKIP')

BUILDENV+=('!check')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
