# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-download-manager-git
_pkgname=lomiri-download-manager
pkgver=r2736.0.1.2
pkgrel=1
pkgdesc='Provides a service for downloading files while an application is suspended'
url='https://gitlab.com/ubports/development/core/lomiri-download-manager'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(ubuntu-download-manager)
provides=(lomiri-download-manager)
depends=(boost dbus-test-runner gtest-lomiri qt5-es2-base qt5-es2-declarative libnih google-glog clang)
makedepends=(git doxygen cmake cmake-extras-lomiri gcovr lcov)
source=('git+https://gitlab.com/ubports/development/core/lomiri-download-manager.git'
        'QtArg.patch')
sha256sums=('SKIP'
            '5c025509bf51586b39fc8881037eb6b9bce173421e8ca6d2f0eae19c350164c7')

BUILDENV+=('!check') # Some tests are broken

prepare() {
  cd ${_pkgname}
  patch -Np1 -i "${srcdir}/QtArg.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations -Wno-error -Wno-error=deprecated" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
