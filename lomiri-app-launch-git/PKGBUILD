# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgbase=lomiri-app-launch
pkgname=(lomiri-app-launch-git)
_pkgname=lomiri-app-launch
pkgver=r3081.0.0.90.r35.g22afcb4
pkgrel=1
pkgdesc='Session init system job for Launching Applications'
url='https://gitlab.com/ubports/development/core/lomiri-app-launch'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL)
depends=(zeitgeist gobject-introspection lttng-ust lomiri-api-git properties-cpp curl click-git)
makedepends=(git cmake  cmake-extras-lomiri dbus-test-runner gtest-lomiri lcov gcovr)
source=('git+https://gitlab.com/ubports/development/core/lomiri-app-launch#branch=ubports/focal_-_miroil-2.0'
        'get-arch.patch'
        'include-array.patch'
        'triplet')
sha256sums=('SKIP'
            '78027e41e375d3ec5819980bf779a3029504def41fb4ace43a3d5338565133cd'
            '8111413cc321ba3283717a48abecec84734e50478ded02747540db3c952ec727'
            'ffd586b2213a62a2bbe91e0fb540749cee379cedf6fc58a405c26982db4731fc')

BUILDENV+=('!check') # Some tests are broken

prepare() {    
    cd ${_pkgname}
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done

    truncate -s 0 tests/CMakeLists.txt

    cp ../../triplet triplet
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  sed -i 's/-std=gnu99/-std=gnu99 -Wno-deprecated-declarations/g' CMakeLists.txt
  sed -i 's/-std=c++11/-std=c++11 -Wno-deprecated-declarations/g' CMakeLists.txt
#  sed -i 's/pkg_check_modules(MIR REQUIRED mirclient)//g' CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
