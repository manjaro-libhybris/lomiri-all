# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=lomiri-app-launch
pkgname=(lomiri-app-launch-git)
_pkgname=lomiri-app-launch
pkgver=r3158.0.1.6.r5.g01675ca
pkgrel=1
pkgdesc='Session init system job for Launching Applications'
url='https://gitlab.com/ubports/development/core/lomiri-app-launch'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL)
depends=(zeitgeist gobject-introspection lttng-ust lomiri-api-git properties-cpp curl click-git)
makedepends=(git cmake  cmake-extras-lomiri dbus-test-runner gtest-lomiri lcov gcovr)
source=('git+https://gitlab.com/ubports/development/core/lomiri-app-launch'
        'get-arch.patch'
        'triplet')
sha256sums=('SKIP'
            '3c79b3a98160f85ee672901ef4abab2657c9ec6a606b7af45ecb007c53a7dd21'
            'ffd586b2213a62a2bbe91e0fb540749cee379cedf6fc58a405c26982db4731fc')

BUILDENV+=('!check') # Some tests are broken

prepare() {
    cd ${_pkgname}
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -N < "../$src"
    done
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cp ../../triplet triplet
  sed -i 's/-std=gnu99/-std=gnu99 -Wno-deprecated-declarations/g' CMakeLists.txt
  sed -i 's/-std=c++11/-std=c++11 -Wno-deprecated-declarations/g' CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
