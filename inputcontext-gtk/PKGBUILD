# Maintainer: Bernhard Landauer <bernhard@manjaro.org>

pkgname=inputcontext-gtk
pkgver=0.99.1.r5.g72d7576
pkgrel=3
pkgdesc="Maliit-keyboard support for gtk3"
arch=('aarch64')
url="https://github.com/maliit/$pkgname"
license=('GPL')
depends=('maliit-framework-git' 'gtk2')
makedepends=('cmake' 'git')
source=("git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  mkdir build
}

build() {
  cd $pkgname/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DLIB_INSTALL_DIR=`pkg-config --variable=libdir gtk+-3.0` \
    -DENABLE_X11=ON
  make
}

package() {
  cd $pkgname/build
  make DESTDIR="$pkgdir" install
}
