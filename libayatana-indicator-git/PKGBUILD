# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname="libayatana-indicator-git"
_pkgname="libayatana-indicator"
pkgver=git_r1878
pkgrel="1"
pkgdesc="Ayatana Indicators (Shared Library)"
arch=("i686" "x86_64" "aarch64")
url="https://github.com/AyatanaIndicators/libayatana-indicator"
license=("LGPL2.1" "LGPL3")
makedepends=("gtk3" "glib2" "ayatana-ido" "mate-common" "git")
depends=("gtk3" "glib2" "ayatana-ido")
options=('!emptydirs')
source=("git+https://github.com/AyatanaIndicators/${_pkgname}.git")
md5sums=("SKIP")
provides=("$_pkgname")
conflicts=("$_pkgname" "${_pkgname}-gtk3")

pkgver()
{
    cd ${_pkgname}
    echo "git_r$(git rev-list --count HEAD)"
}

prepare()
{
    cd ${srcdir}/${_pkgname}
    NOCONFIGURE=1 ./autogen.sh
}

build()
{
    cd ${srcdir}/${_pkgname}
    ./configure --prefix=/usr --with-gtk=3 --disable-tests
    make
}

package()
{
    cd ${srcdir}/${_pkgname}
    make -j1 DESTDIR="${pkgdir}" install
}
