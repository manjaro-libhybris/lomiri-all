# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=telephony-service-git
_pkgname=telephony-service
pkgver=r4843.0.5.1
pkgrel=1
pkgdesc='Telephony Service is a backend dispatcher service for various mobile phone related operations.'
url='https://gitlab.com/ubports/development/core/telephony-service'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL GPL)
conflicts=(telephony-service)
provides=(telephony-service)
depends=(qt5-es2-base qt5-es2-multimedia qt5-pim-git dbus-test-runner libusermetrics-git url-dispatcher-git 
         libphonenumber telepathy-qt telepathy-mission-control history-service-git libnotify
         qt5-feedback lomiri-notifications)
makedepends=(git cmake pulseaudio-hybris)
checkdepends=()
source=('git+https://gitlab.com/ubports/development/core/telephony-service')
sha256sums=('SKIP')

BUILD_DIR=build

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

prepare() {
  cd ${_pkgname}
  sed -i 's/std=c++11/std=c++17/g' CMakeLists.txt
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DWANT_UI_SERVICES=OFF ..
  make
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
