# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=libqtdbustest-git
_pkgname=libqtdbustest
pkgver=r94.f204bb8
pkgrel=1
pkgdesc='Library for testing DBus interactions using Qt'
url='https://gitlab.com/ubports/development/core/libqtdbustest'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(libqtdbustest)
provides=(libqtdbustest)
depends=(gcovr lcov gmock qt5-es2-base)
makedepends=(git cmake cmake-extras-lomiri)
checkdepends=(python-dbusmock python-dbus python-gobject)
source=('git+https://gitlab.com/ubports/development/core/libqtdbustest')
sha256sums=('SKIP')

BUILDENV+=('!check')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make -j8
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
