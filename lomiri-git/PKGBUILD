# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgbase=lomiri
pkgname=(lomiri-git)
_pkgname=lomiri
pkgver=r19098.0.1.2
pkgrel=1
url='https://gitlab.com/ubports/development/core/lomiri'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(lcov gcovr geonames-git lomiri-api-git qmenumodel-git gnome-desktop lomiri-app-launch-git lomiri-ui-toolkit-git libqtdbustest-git libqtdbusmock-git libusermetrics-git liblightdm-qt5 lomiri-settings-components-git lomiri-schemas deviceinfo-git lomiri-notifications-git lomiri-thumbnailer-git hfd-service-git lomiri-download-manager-git indicator-network-git qtmir-git telephony-service-git)
makedepends=(git cmake cmake-extras-lomiri doxygen)
checkdepends=(xorg-server-xvfb)
source=('git+https://gitlab.com/ubports/development/core/lomiri'
        'skip-language-wizard-page.patch'
        'get-arch.patch')
sha256sums=('SKIP'
            'f28deb38ccc6556a749d6d4f0bccd7e6a0166c2880d3fcb78898343c274f92f3'
            'f4337f0149ea47218973e739d75b94deab8a3dbf75ff9f6c7504e4bab4055e81')

BUILDENV+=('!check')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f1
}

prepare() {
    cd ${_pkgname}
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done

    cp ../../triplet triplet

    sed -i "s/lomiri-app-launch-0>=0.1.5/lomiri-app-launch-0/g" CMakeLists.txt
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DNO_TESTS=true ..
  make
}

package_lomiri-git() {
  pkgdesc='Lomiri shell'
  conflicts=(lomiri)
  provides=(lomiri)
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
