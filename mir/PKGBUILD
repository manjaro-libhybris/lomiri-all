# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor: kikadf <kikadf.01@gmail.com>

pkgbase=mir-lomiri
pkgname=(mir)
pkgver=2.3.2
pkgrel=1
pkgdesc="Canonical's display server"
url='https://gitlab.com/ubports/development/core/mir'
arch=(x86_64 i686 aarch64)
license=(GPL LGPL)
depends=(gtest-lomiri boost-libs capnproto google-glog gflags libglvnd  liburcu lttng-ust libepoxy libxml++2.6 nettle libinput libxkbcommon python-pillow freetype2 libevdev protobuf python-dbus python-gobject hicolor-icon-theme libxcursor yaml-cpp)
makedepends=(git glm doxygen cmake boost gcovr gtest-lomiri lcov valgrind python-dbusmock umockdev wlcs-lomiri)
source=("git+https://gitlab.com/ubports/development/core/mir"
        "68480bc9654191b615b0e7b80c8a87599a417470.patch"
        "b8c2da5871a805602f40fc30abe1a6ac619916e2.patch"
        "0dfb6d76d04031441fb4d4fcb2e986f2874004a0.patch"
        "de9b340d823f4effff4c8d2b14b6c30e1f1c3097.patch"
        "45d1b73d74c87974188f578486982a0124f2d286.patch"
        "0543fd7647e52b23a8020851cc4cf235f5ade08b.patch"
        "harness-nullptr.patch")
sha256sums=('SKIP'
            '042862c7007c7fe2dd8f5407331fb1ae4dfba3894f5640d79e8b171659625246'
            '0f2187a2e7a185baaac89dc0060af29642669a54514df8e8637167b1af9e9259'
            '5135fd23978493ae73093b4c6b74f8865a3f419f0f799c4e8a7ed5e98fef9d2b'
            'f2769d8994f6c2c0ee29427575318e505a6fec7c0bca5fe199f0eb3c0df07fed'
            '543395768e847c7b0773e730027bab09f3599ee064033522bbc65c41f844ab51'
            '3970f2c6219fc120452c82f03c2916a26c80d2b85e100efd21836daffa484eaf'
            '9ecacf8498a0e80f5e21d67938452dfca2cdd197db559cd679e32de099590baf')

build() {
    cd mir
    mkdir -p build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations -Wno-error -Wno-error=deprecated -Wno-error=address" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var -DCMAKE_INSTALL_LIBEXECDIR=lib/mir -DMIR_BUILD_INTERPROCESS_TESTS=OFF -DMIR_ENABLE_WLCS_TESTS=OFF -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations -Wno-error -Wno-error=deprecated -Wno-error=unused-parameter -Wno-error=missing-field-initializers" -DMIR_LINK_TIME_OPTIMIZATION=ON -DMIR_USE_LD=ld -DMIR_PLATFORM=mesa-kms\;mesa-x11\;wayland ..
    cmake --build ./
}

prepare() {
    cd mir
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done
}

package_mir() {
    cd mir/build
    make DESTDIR="${pkgdir}/" install

    # cleanup
    rm -f ${pkgdir}/usr/bin/miral-{shell,kiosk,app,terminal,shell,system-compositor}
    rm -f ${pkgdir}/usr/bin/{mir-shell,mir-smoke-test-runner}
    rm -f ${pkgdir}/usr/bin/fake-mir-kiosk
    rm -f ${pkgdir}/usr/share/{applications,wayland-sessions}/miral-shell.desktop
    rm -f ${pkgdir}/usr/share/icons/hicolor/scalable/apps/ubuntu-logo.svg
}
