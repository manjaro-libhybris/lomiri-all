# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor: kikadf <kikadf.01@gmail.com>

pkgbase=mir-lomiri
pkgname=(mir mir-demos)
pkgver=2.3.2
pkgrel=1
pkgdesc="Canonical's display server"
url='https://gitlab.com/ubports/development/core/mir'
arch=(x86_64 i686 aarch64)
license=(GPL LGPL)
depends=(gtest boost-libs capnproto google-glog gflags libglvnd  liburcu lttng-ust libepoxy libxml++2.6 nettle libinput libxkbcommon python-pillow freetype2 libevdev protobuf python-dbus python-gobject hicolor-icon-theme libxcursor yaml-cpp)
makedepends=(git glm doxygen cmake boost gcovr gmock lcov valgrind python-dbusmock umockdev wlcs-lomiri)
optdepends=('qterminal: required for miral demos'
            'ttf-ubuntu-font-family: required for miral demos'
            'qt5-es2-wayland: required for miral demos'
            'xcursor-dmz: opt requirement for miral demos'
            'qtubuntu: opt requirement for miral demos')
source=("git+https://gitlab.com/ubports/development/core/mir"
        "68480bc9654191b615b0e7b80c8a87599a417470.patch"
        "b8c2da5871a805602f40fc30abe1a6ac619916e2.patch"
        "0dfb6d76d04031441fb4d4fcb2e986f2874004a0.patch"
        "de9b340d823f4effff4c8d2b14b6c30e1f1c3097.patch")
sha256sums=('SKIP'
            '042862c7007c7fe2dd8f5407331fb1ae4dfba3894f5640d79e8b171659625246'
            '0f2187a2e7a185baaac89dc0060af29642669a54514df8e8637167b1af9e9259'
            '5135fd23978493ae73093b4c6b74f8865a3f419f0f799c4e8a7ed5e98fef9d2b'
            'f2769d8994f6c2c0ee29427575318e505a6fec7c0bca5fe199f0eb3c0df07fed')

build() {
    cd mir
    mkdir -p build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var -DCMAKE_INSTALL_LIBEXECDIR=lib/mir -DMIR_BUILD_INTERPROCESS_TESTS=OFF -DMIR_ENABLE_WLCS_TESTS=OFF -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations -Wno-error -Wno-error=deprecated -Wno-error=unused-parameter -Wno-error=missing-field-initializers" -DMIR_LINK_TIME_OPTIMIZATION=ON -DMIR_USE_LD=ld -DMIR_PLATFORM=mesa-kms\;mesa-x11\;wayland ..
    cmake --build ./
}

prepare() {
    cd mir
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done
}

package_mir() {
    cd mir/build
    make DESTDIR="${pkgdir}/" install

    # cleanup
    rm -f ${pkgdir}/usr/bin/miral-{shell,kiosk,app,terminal,shell,system-compositor}
    rm -f ${pkgdir}/usr/bin/{mir-shell,mir-smoke-test-runner}
    rm -f ${pkgdir}/usr/bin/fake-mir-kiosk
    rm -f ${pkgdir}/usr/share/{applications,wayland-sessions}/miral-shell.desktop
    rm -f ${pkgdir}/usr/share/icons/hicolor/scalable/apps/ubuntu-logo.svg
}

package_mir-demos() {
    pkgdesc="Canonical's display server (Demos)"
    depends=(mir)
    cd mir/build
    make DESTDIR="${pkgdir}/" install

    # cleanup
    rm -rf ${pkgdir}/usr/share/{mir-perf-framework,wayland-sessions}
    rm -rf ${pkgdir}/usr/{lib,include}
    rm -f ${pkgdir}/usr/bin/mir_*
    rm -f ${pkgdir}/usr/bin/mir{screencast,out,in}
}
