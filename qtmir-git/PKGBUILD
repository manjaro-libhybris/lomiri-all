# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=qtmir-git
pkgname=(qtmir-git)
_pkgname=qtmir
pkgver=r2708.0.7.0
pkgrel=1
url='https://github.com/ubports/qtmir'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(qtmir)
provides=(qtmir)
depends=(qt5-base qt5-quickcontrols qt5-sensors mir process-cpp lomiri-app-launch-git gsettings-qt-git libqtdbustest-git libqtdbusmock-git url-dispatcher-git glm valgrind)
makedepends=(git cmake cmake-extras-git gtest)
source=('git+https://gitlab.com/ubports/development/core/qtmir.git')
sha256sums=('SKIP')

BUILDENV+=('!check')

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DNO_TESTS=1 -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/${BUILD_DIR}
  make ARGS+="--output-on-failure" test
}

package_qtmir-git() {
  pkgdesc='Qt platform abstraction (QPA) plugin for a Mir server'
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
