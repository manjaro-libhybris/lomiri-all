# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-action-api-git
_pkgname=lomiri-action-api
pkgver=r158.3f011e0
pkgrel=1
pkgdesc='Unity Action QML Components'
url='https://gitlab.com/ubports/development/core/lomiri-action-api'
arch=(x86_64 i686 armv7h aarch64)
license=()
depends=(qt5-es2-base qt5-es2-declarative qt5-quickcontrols2)
makedepends=(git cmake cmake-extras-lomiri)
source=('git+https://gitlab.com/ubports/development/core/lomiri-action-api.git#branch=main'
        'disable-libhud.patch')
sha256sums=('SKIP'
            '94bb013fd9ecb474a31e1c4b906fe11c61354ab8c2266f90d185eaa1b9707370')

pkgver() {
 cd ${_pkgname}
 printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${_pkgname}
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -N < "../../$src"
    done
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var
  make -j$(nproc)
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}

