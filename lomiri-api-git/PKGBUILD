# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-api-git
_pkgname=lomiri-api
pkgver=r1145.0.1.1.r18.gfb09560
pkgrel=1
pkgdesc='API for Lomiri shell integration'
url='https://gitlab.com/ubports/development/core/lomiri-api'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(lomiri-api)
provides=(lomiri-api)
depends=(qt5-quickcontrols)
makedepends=(git cmake cppcheck graphviz libqtdbustest-git cmake-extras-lomiri)
source=('git+https://gitlab.com/ubports/development/core/lomiri-api.git#branch=main')
sha256sums=('SKIP')

BUILDENV+=('!check') # Some tests are broken

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" ..
  make
}

check() {
  cd ${_pkgname}/build
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
