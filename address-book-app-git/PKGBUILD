# Maintainer: Furkan Kardame <furkan@fkardame.com>

pkgname=address-book-app-git
_pkgname=address-book-app
pkgver=r2936.5ce4290b
pkgrel=1
epoch=1
pkgdesc='Address Book application'
url='https://github.com/ubports/address-book-app'
arch=(x86_64 i686 armv7h aarch64)
license=()
depends=(history-service-git telephony-service address-book-service-git qt5-base gsettings-qt content-hub-git thumbnailer-git qt5-pim-git ubuntu-ui-toolkit-git qt5-systems libqofono-qt5)
makedepends=(git cmake cmake-extras)
source=('git+https://github.com/ubports/address-book-app.git#branch=xenial_-_qt-5-12'
        0001-Build-qml-plugin-as-shared-lib.patch)
sha256sums=('SKIP'
            '5fce828f4da5a289822e2358c2c3a9effcf5ced1dc570f3c1deae443e86de465')

pkgver() {
 cd ${_pkgname}
 printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_pkgname}
  # patches here
  patch -p1 -i ../0001-Build-qml-plugin-as-shared-lib.patch
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var
  make

}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
  mv "${pkgdir}/usr/lib/qt5/" "${pkgdir}/usr/lib/qt/"
}
