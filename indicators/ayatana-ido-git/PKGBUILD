# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=ayatana-ido-git
_pkgname="ayatana-ido"
pkgver=git_r771
pkgrel=1
pkgdesc='Ayatana Indicator Display Objects'
arch=('x86_64' 'aarch64')
url='https://github.com/AyatanaIndicators/ayatana-ido'
license=('LGPL2.1' 'LGPL3')
depends=('gtk3' 'glib2')
makedepends=('mate-common' 'gtk-doc' 'gobject-introspection' 'vala' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/AyatanaIndicators/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    echo "git_r$(git rev-list --count HEAD)"
}

build() {
  cd "${_pkgname}"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" ..
  make
}

package() {
  cd "${_pkgname}"/build
  make DESTDIR="$pkgdir/" install
  find ${pkgdir}/usr/lib -name *.la -delete
}
