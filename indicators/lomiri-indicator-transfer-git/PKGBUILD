# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname="lomiri-indicator-transfer-git"
_pkgname="indicator-transfer"
pkgver=git_r383
pkgrel=1
pkgdesc="Lomiri Transfer Indicator"
arch=("i686" "x86_64" "aarch64")
url="https://gitlab.com/ubports/development/core/indicator-transfer"
license=("GPL2" "GPL3")
depends=("systemd" "libnotify" "dconf" "lomiri-app-launch-git")
makedepends=("cmake" "cmake-extras-lomiri" "intltool" "glib2" "dbus" "debianutils" "properties-cpp" "click")
source=("git+https://gitlab.com/ubports/development/core/indicator-transfer")
sha256sums=('SKIP')
options=("!emptydirs")
provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
    cd ${_pkgname}
    echo "git_r$(git rev-list --count HEAD)"
}

build() {
    cd ${_pkgname}
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib -Denable_tests=OFF
    make
}

package() {
    cd ${_pkgname}
    make DESTDIR="${pkgdir}" install
}
