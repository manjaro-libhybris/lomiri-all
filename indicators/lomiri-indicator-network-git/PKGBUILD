# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=indicator-network-git
_pkgname=lomiri-indicator-network
pkgver=r2442.1.0.0
pkgrel=1
pkgdesc='Network indicator'
url='https://gitlab.com/ubports/development/core/lomiri-indicator-network'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3 GPL3)
conflicts=(indicator-network)
provides=(indicator-network)
depends=(lcov gcovr gsettings-qt ofono libqofono-qt5 libqtdbustest-git libqtdbusmock-git gmenuharness-bzr url-dispatcher-git)
makedepends=(git cmake cmake-extras-lomiri doxygen)
source=('git+https://gitlab.com/ubports/development/core/lomiri-indicator-network.git'
        'fix-systemd.patch')
sha256sums=('SKIP'
            '4f2a63e719e103ad8c8b1ff14d4c2fe1dabe904e0ab522a0d64230c6278cc131')
BUILDENV+=('!check') # Hangs

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

prepare() {
    cd ${_pkgname}

    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../$src"
    done  
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" ..
  make
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
