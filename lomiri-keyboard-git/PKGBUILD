# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-keyboard-git
_pkgname=lomiri-keyboard
pkgver=r2312.gf8c368dd
pkgrel=1
pkgdesc="Lomiri Maliit Keyboard"
arch=('aarch64')
url="https://gitlab.com/ubports/development/core/lomiri-keyboard"
license=('GPL')
groups=()
depends=('maliit-framework' 'qt5-feedback' 'qt5-es2-multimedia' 'gsettings-qt-git'
         'hunspell' 'libchewing' 'presage-git' 'libpinyin' 'sqlite' 'anthy')
makedepends=('git' 'cmake')
source=("git+https://gitlab.com/ubports/development/core/lomiri-keyboard"
        "e8bbad0d78eb2ca75b47e0d9e71b3c40c2e268bc.patch")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname
    patch -Np1 -i "${srcdir}/e8bbad0d78eb2ca75b47e0d9e71b3c40c2e268bc.patch"
}

build() {
    cd ${_pkgname}
    qmake CONFIG+=no_docs CONFIG+=notests INCLUDEPATH+="/usr/include/maliit-2" QMAKE_CXXFLAGS+="-I/usr/include/maliit-2 -std=c++14"
    make
}

package() {
    cd ${_pkgname}
    make INSTALL_ROOT="${pkgdir}/" install
}
