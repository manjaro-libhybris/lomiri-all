# Maintainer: Furkan Kardame <furkan@fkardame.com>

pkgname=lomiri-gallery-app-git
_pkgname=gallery-app
pkgver=r3915.3eded6d0
pkgrel=1
pkgdesc="Lomiri Photo gallery application"
arch=('x86_64' 'armv7' 'aarch64')
url="https://gitlab.com/ubports/apps/gallery-app"
license=('GPL3')
depends=(qt5-base qt5-quickcontrols exiv2 libmediainfo)
makedepends=('git' 'cmake' 'intltool')
provides=(lomiri-gallery-app)
conflicts=(lomiri-gallery-app)
source=('git+https://gitlab.com/ubports/apps/gallery-app.git')
md5sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  cd ${_pkgname}

  # Ignore warnings for now
  sed "s/-Werror//g" -i CMakeLists.txt
}

build() {
  cd build
  cmake ../${_pkgname} . -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var -DCLICK_MODE=off
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
