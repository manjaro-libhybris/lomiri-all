# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=qmenumodel-git
_pkgname=qmenumodel
pkgver=r571.0.9.1.r2.g24cc4ee
pkgrel=1
pkgdesc='Qt binding for GMenuModel'
url='https://github.com/AyatanaIndicators/qmenumodel'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(qmenumodel)
provides=(qmenumodel)
depends=(qt5-base qt5-quickcontrols)
makedepends=(git cmake)
source=('git+https://github.com/AyatanaIndicators/qmenumodel.git')
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" .
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
