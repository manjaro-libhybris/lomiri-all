# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=url-dispatcher
pkgname=(url-dispatcher-git)
_pkgname=lomiri-url-dispatcher
pkgver=r939.0.1.2.r16.g23c4235
pkgrel=1
pkgdesc='Service to allow sending of URLs and get handlers started'
url='https://gitlab.com/ubports/development/core/lomiri-url-dispatcher'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(url-dispatcher)
provides=(url-dispatcher)
depends=(lomiri-app-launch-git intltool apparmor)
makedepends=(git cmake cmake-extras-lomiri dbus-test-runner lcov gcovr gtest-lomiri)
checkdepends=(python-testtools python-nose python-dbusmock)
source=('git+https://gitlab.com/ubports/development/core/lomiri-url-dispatcher#branch=main')
sha256sums=('SKIP')

prepare() {
  cd ${_pkgname}

  truncate -s 0 tests/CMakeLists.txt
  sed -i 's/-Wall/-Wall -Wno-deprecated-declarations/g' CMakeLists.txt
  sed -i 's/lomiri-app-launch-0>=0.1.3/lomiri-app-launch-0/g' CMakeLists.txt
  sed -i 's#option (enable_mirclient "Enable overlay using mirclient" ON)#option (enable_mirclient "Enable overlay using mirclient" OFF)#g' CMakeLists.txt
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

package_url-dispatcher-git() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
