# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=url-dispatcher
pkgname=(url-dispatcher-git url-dispatcher-gui-git)
_pkgname=lomiri-url-dispatcher
pkgver=r770.0.1.0.r29.gc71438a
pkgrel=1
pkgdesc='Service to allow sending of URLs and get handlers started'
url='https://gitlab.com/ubports/development/core/lomiri-url-dispatcher'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(url-dispatcher)
provides=(url-dispatcher)
depends=(lomiri-app-launch-git intltool apparmor)
makedepends=(git cmake cmake-extras-git dbus-test-runner lcov gcovr gmock)
checkdepends=(python-testtools python-nose python-dbusmock)
source=('git+https://gitlab.com/ubports/development/core/lomiri-url-dispatcher#branch=main'
        '0001-Add-missing-array-includes.patch')
sha256sums=('SKIP'
            'bf2cda8e5306dceb1170da8919c9ef0950d81738c3485fa4ef461b09e66d2c9a')

prepare() {
  cd ${_pkgname}

  sed -i 's/-Wall/-Wall -Wno-deprecated-declarations/g' CMakeLists.txt
  
  for i in "${srcdir}/"*.patch; do
      patch -Np1 -i "$i"
  done
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

package_url-dispatcher-git() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install

}

package_url-dispatcher-gui-git() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install

}
# vim:set ts=2 sw=2 et:
