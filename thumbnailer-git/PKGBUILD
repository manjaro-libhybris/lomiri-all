# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=lomiri-thumbnailer-git
_pkgname=lomiri-thumbnailer
pkgver=r1978.3.0.0.r6.g5ea5dc7
pkgrel=1
pkgdesc='D-Bus service for out of process thumbnailing'
url='https://gitlab.com/ubports/development/core/lomiri-thumbnailer'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL)
conflicts=(lomiri-thumbnailer)
provides=(lomiri-thumbnailer)
depends=(boost qt5-base qt5-quickcontrols gstreamer gst-plugins-base gdk-pixbuf2 libexif lomiri-api-git apparmor taglib persistent-cache-cpp-git)
makedepends=(git cmake)
source=('git+https://gitlab.com/ubports/development/core/lomiri-thumbnailer.git#branch=main'
        'mark-inline.patch'
        '0001-Add-missing-array-includes.patch'
        '0002-Disable-tests.patch')
sha256sums=('SKIP'
            '536c4060caa9eebc8ae53d60f2c431f5d9eee33e0e2e4974529801f058d3b2b7'
            '5264a03e4c46361cf6b67669844ca341bbb986aa888dd755ba24670501fcd0a8'
            'd42e67afe17a9005e43964bec0c6a597719ff46fe32d5ed7493435a0c29ad4ae')

prepare() {
  cd ${_pkgname}

  patch -Np1 -i "${srcdir}/0001-Add-missing-array-includes.patch"
  patch -Np1 -i "${srcdir}/0002-Disable-tests.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" .
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
