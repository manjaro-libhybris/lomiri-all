# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=persistent-cache-cpp-git
_pkgname=persistent-cache-cpp
pkgver=r125.1.0.5.r7.gb93792f
pkgrel=1
pkgdesc='A cache of key-value pairs with persistent storage for C++'
url='https://gitlab.com/ubports/core/lib-cpp/persistent-cache-cpp'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(persistent-cache-cpp)
provides=(persistent-cache-cpp)
depends=(boost leveldb valgrind)
makedepends=(git cmake cmake-extras-git doxygen graphviz lcov gcovr gtest gmock)
source=("git+https://gitlab.com/ubports/core/lib-cpp/persistent-cache-cpp.git"
        "0001-Add-missing-array-includes.patch")
sha256sums=('SKIP'
            '0b88e744ad60335b22e848a6ce05b929e7ddcb1ae97fb3aac2406303a191887f')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

prepare() {
    cd ${_pkgname}
    for i in "${srcdir}/"*.patch; do
        patch -Np1 -i "$i"
    done
}

#check() {
#  cd ${_pkgname}
#  make ARGS+="--output-on-failure" test
#}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
