# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=history-service-git
_pkgname=history-service
pkgver=r1521.0.4
pkgrel=1
pkgdesc='History service to store messages and calls'
url='https://gitlab.com/ubports/development/core/history-service'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL)
conflicts=(history-service)
provides=(history-service)
depends=(lcov gcovr libphonenumber telepathy-qt sqlite telepathy-mission-control qt-gstreamer qt5-pim-git)
makedepends=(git cmake)
source=('git+https://gitlab.com/ubports/development/core/history-service')
sha256sums=('SKIP')

# --   Package 'QtGLib-2.0', required by 'virtual:world', not found (qt-gstreamer)
BUILDENV+=('!check')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations -Wno-error -Wno-error=deprecated -std=c++14" -DCMAKE_INSTALL_LIBDIR="lib/" .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
