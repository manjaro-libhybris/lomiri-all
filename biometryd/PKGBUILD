# Maintainer: Marius Gripsgard <marius@ubports.com>

pkgname=biometryd-git
_pkgname=biometryd
pkgver=r232.0.3.0
pkgrel=1
pkgdesc='Fingerprint deamon'
url='https://gitlab.com/ubports/development/core/biometryd'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL GPL)
conflicts=(biometryd)
provides=(biometryd)
depends=(qt5-es2-base qt5-es2-declarative dbus-cpp apparmor process-cpp properties-cpp boost-libs libelf sqlite)
makedepends=(git cmake boost cmake-extras-lomiri gtest-lomiri)
checkdepends=()
source=('git+https://gitlab.com/ubports/development/core/biometryd'
        'disable-tests.patch')
sha256sums=('SKIP'
            '999513331347eae5a6eb64f67a19be77f1af0949f6cf93cf86fe1585cd91ae40')

BUILD_DIR=build

prepare() {
    cd biometryd/tests
    local src
    for src in "${source[@]}"; do
      src="${src%%::*}"
      src="${src##*/}"
      [[ $src = *.patch ]] || continue
      echo "Applying patch $src..."
      patch -Np1 < "../../$src"
    done
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | cut -d "-" -f 1
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  CXXFLAGS=-Wno-error=maybe-uninitialized cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" -DBUILD_TESTING=OFF ..
  make -j$(nproc)
}

package() {
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
  mv ${pkgdir}/lib/systemd/ ${pkgdir}/usr/lib/
  rm ${pkgdir}/lib -r
  sed -i 's/lxc-android-config.service/lxc@android.service/g' ${pkgdir}/usr/lib/systemd/system/biometryd.service
}
