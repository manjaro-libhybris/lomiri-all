# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=libqofono-qt5
pkgver=0.120
pkgrel=1
pkgdesc="A library for accessing the ofono daemon, and a declarative plugin for it. This allows accessing ofono in qtquick and friends."
arch=('aarch64')
url="https://github.com/sailfishos/libqofono"
license=('LGPL')
depends=('qt5-es2-declarative' 'qt5-xmlpatterns')
provides=("libqofono-qt5")
source=("https://github.com/sailfishos/libqofono/archive/refs/tags/${pkgver}.tar.gz"
        "context-preferred.patch"
        "mtk_settings_binding.patch")
sha256sums=('SKIP'
            'f793347078553903c7e6f6b235091c1863b18cd9b4078f34addc7a3ded7f2bd8'
            '1f63b04d05be4988498cba96c9700d6f4b6237325d65e508cf6fa2f21247a765')

prepare() {
    cd libqofono-$pkgver
    mkdir -p build

    patch -Np1 -i "${srcdir}/context-preferred.patch"
    patch -Np1 -i "${srcdir}/mtk_settings_binding.patch"
}

build() {
    cd libqofono-$pkgver/build
    qmake-qt5 PREFIX=/usr ../libqofono.pro
    make
}

package() {
    cd libqofono-$pkgver/build
    make INSTALL_ROOT="$pkgdir/" install
}
