# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-docviewer-app-git
_pkgname=docviewer-app
pkgver=r1730.2a5008f
pkgrel=1
pkgdesc="Lomiri documents viewer"
arch=('x86_64' 'armv7' 'aarch64')
url="https://gitlab.com/ubports/apps/docviewer-app"
license=('GPL3')
depends=(qt5-es2-base qt5-es2-declarative poppler-qt5)
makedepends=('git' 'cmake' 'intltool' 'libreoffice-fresh-sdk')
provides=(lomiri-docviewer-app)
conflicts=(lomiri-docviewer-app)
source=('git+https://gitlab.com/ubports/apps/docviewer-app.git')
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${_pkgname}
  cmake ../${_pkgname} . -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var -DCLICK_MODE=off
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="$pkgdir/" install

  # install missing icon
  cp -v $srcdir/${_pkgname}/data/docviewer-app.png $pkgdir/usr/share/ubuntu-docviewer-app/docviewer-app.png
}
