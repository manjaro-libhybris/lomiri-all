# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=camera-app-git
_pkgname=camera-app
pkgver=r2760.82ea258
pkgrel=1
pkgdesc="Camera application"
arch=('x86_64' 'armv7' 'aarch64')
url="https://gitlab.com/ubports/apps/camera-app"
license=('GPL3')
depends=(qt5-es2-base qt5-es2-multimedia qt5-quickcontrols2 exiv2 lomiri-ui-toolkit-git
         libusermetrics-git lomiri-action-api-git gst-plugins-bad-hybris gst-plugins-bad-libs-hybris)
makedepends=('git' 'cmake' 'extra-cmake-modules' 'intltool')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${_pkgname}
  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCLICK_MODE=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="$pkgdir/" install
}
