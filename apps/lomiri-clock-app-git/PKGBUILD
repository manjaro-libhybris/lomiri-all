# Maintainer: Bardia Moshiri <fakeshell@bardia.tech>

pkgname=lomiri-clock-app-git
_pkgname=clock-app
pkgver=r2857.acbfa82b
pkgrel=1
pkgdesc="Lomiri clock app"
arch=('x86_64' 'armv7' 'aarch64')
url="https://gitlab.com/ubports/apps/clock-app"
license=('GPL3')
depends=(qt5-es2-base qt5-es2-declarative lomiri-ui-toolkit-git u1db-qt libusermetrics)
makedepends=('git' 'cmake' 'intltool')
provides=(lomiri-clock-app)
conflicts=(lomiri-clock-app)
source=('git+https://gitlab.com/ubports/apps/clock-app.git')
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${_pkgname}
  cmake ../${_pkgname} . -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var -DCLICK_MODE=off
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="$pkgdir/" install
}
