# Maintainer: Marius Gripsgard <marius@ubports.com>

pkgname=suru-icon-theme-git
_pkgname=suru-icon-theme
pkgver=1
pkgrel=1
pkgdesc="Lomiri monochrome icon theme, Ambiance and Radiance themes, and Lomiriartwork. Based on Ubuntu themes"
arch=(any)
url="https://gitlab.com/ubports/development/core/suru-icon-theme"
license=(CCPL)
depends=(humanity-icon-theme adwaita-icon-theme hicolor-icon-theme
         gtk-engine-murrine)
makedepends=(imagemagick icon-naming-utils)
provides=(light-themes ubuntu-mono ubuntu-themes)
conflicts=(light-themes ubuntu-mono ubuntu-themes)
source=("git+https://gitlab.com/ubports/development/core/suru-icon-theme")
md5sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  # Install themes
  cd ${_pkgname}
  install -dm755   "${pkgdir}/usr/share/themes/"
  cp -av Ambiance/ "${pkgdir}/usr/share/themes/"
  cp -av Radiance/ "${pkgdir}/usr/share/themes/"

  # Install icons
  install -dm755            "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark/  "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light/ "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons/        "${pkgdir}/usr/share/icons/"
  cp -av suru-icons/        "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mobile/     "${pkgdir}/usr/share/icons/"

  mv "${pkgdir}"/usr/share/icons/{suru-icons,suru}/

  install -Dm644 distributor-logo.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/distributor-logo.png"
}
