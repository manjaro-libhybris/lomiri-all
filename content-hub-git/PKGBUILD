# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=content-hub-git
_pkgname=content-hub
pkgver=r1183.9a56dc8
pkgrel=1
epoch=1
pkgdesc='Content sharing/picking infrastructure and service'
url='https://gitlab.com/ubports/development/core/content-hub'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(content-hub)
provides=(content-hub)
depends=(lcov gcovr gmock lomiri-app-launch-git lomiri-download-manager-git gsettings-qt apparmor libnotify lomiri-ui-toolkit-git)
makedepends=(git cmake cmake-extras-lomiri doxygen graphviz) 
checkdepends=(xorg-server-xvfb)
source=('git+https://gitlab.com/ubports/development/core/content-hub')
sha256sums=('SKIP')

BUILDENV+=('!check')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd ${_pkgname}

  sed -i 's/ubuntu-app-launch-2/ubuntu-app-launch-3/g' CMakeLists.txt
}

build() {
  cd ${_pkgname}
  mkdir -p /tmp/runtime-builduser
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" -DENABLE_TESTS=off .
  XDG_RUNTIME_DIR=/tmp/runtime-builduser make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
