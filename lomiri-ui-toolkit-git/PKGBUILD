# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=lomiri-ui-toolkit-git
_pkgname=lomiri-ui-toolkit
pkgver=r18466.1.3.5010.r15.gf0323d488
pkgrel=1
epoch=2
pkgdesc='Qt Components for Ubuntu'
url='https://gitlab.com/ubports/development/core/lomiri-ui-toolkit'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
depends=(qt5-es2-base qt5-quickcontrols lttng-ust libnih qt5-systems qt5-pim qt5-svg qt5-feedback suru-icon-theme-git qt5-graphicaleffects bluez-libs)
makedepends=(git)
source=('git+https://gitlab.com/ubports/development/core/lomiri-ui-toolkit.git'
        'NoWarns.patch'
        'dpkg.patch')
sha256sums=('SKIP'
            '3b8a3165161d5c695a7f3f8f3b08a3de11451b8f52276b5b1856730f0aa557c9'
            '5851aec4b72ddd1556494ab188c6de64ccaa77a1b574a0bd2631916300ac242a')

BUILDENV+=('!check') # It needs a DISPLAY, and I am too tired to set it up now

prepare() {
  cd ${_pkgname}

  patch -Np1 -i "${srcdir}/NoWarns.patch"
  patch -Np1 -i "${srcdir}/dpkg.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  qmake CONFIG+=no_docs
  make
}

package() {
  cd ${_pkgname}
  mkdir -p /tmp/runtime-builduser
  XDG_RUNTIME_DIR=/tmp/runtime-builduser make INSTALL_ROOT="${pkgdir}/" install || XDG_RUNTIME_DIR=/tmp/runtime-builduser make INSTALL_ROOT="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
