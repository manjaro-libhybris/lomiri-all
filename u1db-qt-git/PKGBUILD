# Maintainer: Furkan Kardame <furkan@fkardame.com>

pkgbase=u1db-qt-git
pkgname=(u1db-qt-git u1db-qt-demos-git)
_pkgname=u1db-qt
pkgver=r414.8dd87b9
pkgrel=3
pkgdesc="U1 database for qt"
arch=('x86_64' 'armv7' 'aarch64')
url="https://github.com/ubports/u1db-qt"
license=('GPL3')
depends=(qt5-base qt5-declarative)
makedepends=('git' 'cmake' 'intltool')
source=('git+https://github.com/ubports/u1db-qt.git'
        'https://github.com/ubports/u1db-qt/pull/1.patch')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd ${_pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  cd ${_pkgname}

  patch -Np1 -i "${srcdir}/1.patch"
}

build() {
  cd build
  cmake ../${_pkgname} . -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LOCALSTATEDIR=/var
  make
}

package_u1db-qt-git() {
  provides=(u1db-qt)
  conflicts=(u1db-qt)
  cd build
  make DESTDIR="$pkgdir/" install

  # cleanup
  rm -Rf $pkgdir/usr/share/{applications,u1db-qt}
}

package_u1db-qt-demos-git() {
  pkgdesc="U1 database for qt (Demos)"
  depends=(u1db-qt-git)
  cd build
  make DESTDIR="$pkgdir/" install

  # cleanup
  rm -Rf $pkgdir/usr/share/qtcreator
  rm -Rf $pkgdir/usr/{lib,include}
}
